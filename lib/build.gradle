buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.4.12"
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 23
        versionCode System.getenv("BUILD_NUMBER") as Integer ?: 1
        versionName "1." + android.defaultConfig.versionCode
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }
}

ext {
    mavenVersion = "1." + System.getenv("BUILD_NUMBER")
}

dependencies {
}


publishing {
   publications {
        aar(MavenPublication) {
            groupId 'com.wire'
            version mavenVersion
            artifactId 'wiretranslations'
            artifact 'build/outputs/aar/lib-release.aar'
            artifact javadocJar
            artifact sourcesJar
        }
    }
}

artifactory {
    contextUrl = 'https://wire.jfrog.io/wire'
    publish {
        repository {
            // The Artifactory repository key to publish to
            repoKey = 'libs-release-local'

            username = System.getenv("ARTIFACTORY_USER")
            password = System.getenv("ARTIFACTORY_PASSWORD")
        }
        defaults {
            publications('aar')
            publishArtifacts = true
            publishPom = true
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
